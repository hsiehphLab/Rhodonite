import os
import sys
import math
import pandas as pd
from snakemake.utils import min_version

min_version("6.0")

S_DIR = os.path.realpath(os.path.dirname(srcdir("Snakefile")))
shell.prefix(f"set -eo pipefail;")


TEMP_DIR = config.pop("tempdir", "temp")
if TEMP_DIR != "temp":
    if os.path.exists("temp"):
        if os.path.islink("temp") and os.path.realpath("temp") == os.path.realpath(
            TEMP_DIR
        ):
            print("The temp dir has already been linked.")
        else:
            sys.exit("temp/ already in use, please move it before running.")
    else:
        shell("ln -s {TEMP_DIR} temp")


df = pd.read_csv(config["test"]["ref"] + ".fai", sep="\t", header=None)
n_records = df.shape[0]


scattergather:
    fasta=n_records,


include: "rules/split.smk"
include: "rules/RepeatMasker.smk"
include: "rules/DupMasker.smk"
include: "rules/trf.smk"


rule all:
    input:
        fasta=gather.fasta(rules.split_fasta.output.fasta, sample="test",),
        trf=expand(rules.trf.output, sample="test"),
        RepeatMasker=expand(rules.RepeatMasker.output, sample="test"),
        DupMasker=expand(rules.DupMasker.output, sample="test"),
